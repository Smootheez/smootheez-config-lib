plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

group = project.maven_group
version = project.mod_version

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

}

processResources{
    inputs.property "version", project.version
    filteringCharset "UTF-8"
    filesMatching("fabric.mod.json"){
        expand project.properties
    }
}

def targetJavaVersion = project.java_version
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= (10 as String) || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion as Integer)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}



publishing {
    publications {
        mod(MavenPublication) {
            groupId = maven_group
            artifactId = archives_base_name
            version = mod_version

            from components.java
        }
    }

    def localProperties = new Properties()
    def localPropertiesFile = rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        localProperties.load(new FileInputStream(localPropertiesFile))
    }

    repositories {
        maven {
            url localProperties.getProperty('localRepositoryUrl', '')
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'zLVNfWvA'
    versionNumber = project.mod_version
    versionName = "${project.mod_name} ${project.mod_version}"
    versionType = 'release'
    uploadFile = remapJar
    gameVersions = ['1.21', '1.21.1']
    loaders = ['fabric']
}

curseforge {
    apiKey = "${System.getenv("CURSEFORGE_TOKEN")}"
    project {
        id = '1107934'
        releaseType = 'release'
        addGameVersion '1.21'
        addGameVersion '1.21.1'

        addGameVersion 'Fabric'
        addGameVersion 'Java 21'

        mainArtifact(remapJar) {
            displayName = "${project.mod_name} ${project.mod_version}"
        }
    }
}

tasks.register('publishMod') {
    dependsOn 'modrinth', 'curseforge'
    group = 'publishing'
}